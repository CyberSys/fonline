cmake_minimum_required( VERSION 3.10.2 )
project( FOnlineProject )

# Options
set( FONLINE_VERBOSE_BUILD OFF CACHE BOOL "Verbose build mode" )
set( FONLINE_OUTPUT_BINARIES_PATH "${CMAKE_BINARY_DIR}" CACHE PATH "Binaries output path" )
set( FONLINE_WEB_DEBUG OFF CACHE BOOL "Web debug build" )
set( FONLINE_BUILD_CLIENT ON CACHE BOOL "Build client binaries" )
set( FONLINE_BUILD_SERVER OFF CACHE BOOL "Build server binaries" )
set( FONLINE_BUILD_MAPPER OFF CACHE BOOL "Build mapper binaries" )
set( FONLINE_BUILD_BAKER OFF CACHE BOOL "Build baker binaries" )
set( FONLINE_UNIT_TESTS OFF CACHE BOOL "Build only binaries for Unit Testing" )
set( FONLINE_CODE_COVERAGE OFF CACHE BOOL "Build only binaries for Code Coverage reports" )
set( FONLINE_CUSTOM_VERSION "" CACHE STRING "Custom version for build (integer FO_VERSION in code), use hex digits (0-9, a-f), maximum are 8" )
set( FONLINE_NATIVE_SCRIPTING ON CACHE BOOL "Supporting of native scripting" )
set( FONLINE_ANGELSCRIPT_SCRIPTING ON CACHE BOOL "Supporting of AngelScript scripting" )
set( FONLINE_MONO_SCRIPTING ON CACHE BOOL "Supporting of Mono scripting" )
set( FONLINE_GENERATE_PACKAGING_INFO OFF CACHE BOOL "Packaging information generation" )
set( FONLINE_CMAKE_CONTRIBUTION "" CACHE STRING "CMake contribution file" )

# Quiet all non-error messages instead ourself
function( message mode )
	if( ${mode} STREQUAL "FATAL_ERROR" )
		_message( FATAL_ERROR ${ARGN} )
	elseif( ${mode} STREQUAL "SEND_ERROR" )
		_message( SEND_ERROR ${ARGN} )
	elseif( FONLINE_VERBOSE_BUILD )
		_message( ${mode} ${ARGN} )
	endif()
endfunction()
function( StatusMessage )
	_message( STATUS ${ARGN} )
endfunction()
function( AbortMessage )
	_message( FATAL_ERROR ${ARGN} )
endfunction()
if( FONLINE_VERBOSE_BUILD )
	StatusMessage( "Verbose build mode" )
	set( CMAKE_VERBOSE_MAKEFILE ON CACHE BOOL "Forced by FOnline" FORCE )
else()
	set( CMAKE_VERBOSE_MAKEFILE OFF CACHE BOOL "Forced by FOnline" FORCE )
endif()

# Build options
StatusMessage( "Start project generation" )

# Global options
set( BUILD_SHARED_LIBS OFF CACHE BOOL "Forced by FOnline" FORCE )
set( BUILD_TESTING OFF CACHE BOOL "Forced by FOnline" FORCE )

# Temporary variables
set( FO_OS "" )
set( FO_OS_UPPER "" )
set( FO_BUILD_CONFIGURATION "" )
set( FO_BUILD_CLIENT_LIBRARY NO )
set( FO_BUILD_OPTIONS "" )
set( FO_BUILD_LINKER_OPTIONS "" )
set( FO_BUILD_DEFINES "" )
set( FO_CLIENT_OUTPUT "" )
set( FO_SERVER_OUTPUT "" )
set( FO_TOOLS_OUTPUT "" )
set( FO_TESTS_OUTPUT "" )
set( FO_CLIENT_POSTFIX "" )
set( FO_NON_CLIENT_POSTFIX "" )
set( FO_COMMON_SYSTEM_LIBS "" )
set( FO_COMMON_LIBS "" )
set( FO_SERVER_SYSTEM_LIBS "" )
set( FO_SERVER_LIBS "" )
set( FO_CLIENT_SYSTEM_LIBS "" )
set( FO_CLIENT_LIBS "" )
set( FO_RENDER_SYSTEM_LIBS "" )
set( FO_RENDER_LIBS "" )
set( FO_BAKER_SYSTEM_LIBS "" )
set( FO_BAKER_LIBS "" )
set( FO_USE_GLEW NO )
set( FO_OUTPUT_BINARIES_PATH "" )
set( FO_GIT_HASH_RESULT "" )
set( FO_GIT_DIFF_RESULT "" )
set( FO_GIT_HASH "" )
set( FO_VERSION_CONTENT "" )
set( FO_SDL_DIR "" )
set( FO_ZLIB_DIR "" )
set( FO_PNG_DIR "" )
set( FO_OGG_DIR "" )
set( FO_VORBIS_DIR "" )
set( FO_THEORA_DIR "" )
set( FO_ACM_DIR "" )
set( FO_SHA_DIR "" )
set( FO_GLEW_DIR "" )
set( FO_NCODE_HOOK_DIR "" )
set( FO_ASSIMP_DIR "" )
set( FO_FBXSDK_DIR "" )
set( FO_CURL_DIR "" )
set( FO_JSON_DIR "" )
set( FO_FMT_DIR "" )
set( FO_ASIO_DIR "" )
set( FO_WEBSOCKETS_DIR "" )
set( FO_BSON_DIR "" )
set( FO_MONGODB_DIR "" )
set( FO_UNQLITE_DIR "" )
set( FO_VARIANT_DIR "" )
set( FO_OPENSSL_DIR "" )
set( FO_DEAR_IMGUI_DIR "" )
set( FO_CATCH2_DIR "" )
set( FO_BACKWARDCPP_DIR "" )
set( FO_SPARK_DIR "" )
set( FO_GLSLANG_DIR "" )
set( FO_SPIRV_CROSS_DIR "" )
set( FO_ANGELSCRIPT_DIR "" )
set( FO_ANGELSCRIPT_EXT_DIR "" )
set( FO_ANGELSCRIPT_SDK_DIR "" )
set( FO_ANGELSCRIPT_PREPROCESSOR_DIR "" )
set( FO_MONO_DIR "" )
set( FO_OGG_SOURCE "" )
set( FO_VORBIS_SOURCE "" )
set( FO_THEORA_SOURCE "" )
set( FO_GLEW_SOURCE "" )
set( FO_IMGUI_SOURCE "" )
set( FO_COMMON_SOURCE "" )
set( FO_SERVER_SOURCE "" )
set( FO_CLIENT_SOURCE "" )
set( FO_TOOLS_SOURCE "" )
set( FO_SCRIPTING_SOURCE "" )
set( FO_CORE_LIBS_GROUP "" )
set( FO_APPLICATIONS_GROUP "" )
set( FO_CACHE_VARIABLES "" )
set( FO_CONTRIBUTION_DIR "" )
set( FO_GENERATED_INCLUDES "" )
set( FO_CONTENT "" )
set( FO_RESOURCES "" )
set( FO_RAW_RESOURCES "" )
set( FO_SPRITE_ATLAS "" )
set( FO_SCRIPT_API "" )
set( FO_ANGELSCRIPT_FILES "" )
set( FO_MONO_ASSEMBLIES "" )
set( FO_CONFIGS "" )
set( FO_PACKAGES "" )

# Architecture configuration
if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
	StatusMessage( "CPU architecture: 64-bit" )
elseif( CMAKE_SIZEOF_VOID_P EQUAL 4 )
	StatusMessage( "CPU architecture: 32-bit" )
else()
	message( FATAL_ERROR "Invalid pointer size, nor 8 or 4 bytes" )
endif()

if( WIN32 AND NOT WINRT )
	StatusMessage( "Operating system: Windows" )
	set( FO_OS "Windows" )
	set( FO_BUILD_CONFIGURATION "RelWithDebInfo" )
	if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
		set( FO_CLIENT_POSTFIX "64" )
	else()
		set( FO_NON_CLIENT_POSTFIX "32" )
	endif()
	set( FO_USE_GLEW YES )
	list( APPEND FO_BUILD_OPTIONS "/MP" )
	if( FONLINE_BUILD_CLIENT )
		list( APPEND FO_BUILD_OPTIONS "/MT" )
	else()
		list( APPEND FO_BUILD_OPTIONS "/MD" )
	endif()
	# Debug: /MTd /MP /RTCc /RTCsu
	list( APPEND FO_BUILD_LINKER_OPTIONS "/INCREMENTAL:NO" )
	list( APPEND FO_BUILD_DEFINES "UNICODE" "_UNICODE" "_CRT_SECURE_NO_WARNINGS" "_CRT_SECURE_NO_DEPRECATE" "_WINSOCK_DEPRECATED_NO_WARNINGS" )
	list( APPEND FO_COMMON_SYSTEM_LIBS "user32" "ws2_32" "version" "winmm" "imm32" "glu32" "dbghelp" "psapi" "d3d9" "d3d11" "gdi32" "xinput" "opengl32" )
elseif( WIN32 AND WINRT )
	StatusMessage( "Operating system: Universal Windows Platform" )
	set( FO_OS "Windows" )
	set( FO_BUILD_CONFIGURATION "RelWithDebInfo" )
	if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
		set( FO_CLIENT_POSTFIX "_UWP64" )
	else()
		set( FO_CLIENT_POSTFIX "_UWP32" )
	endif()
	set( FO_USE_GLEW NO )
	list( APPEND FO_BUILD_OPTIONS "/MD" "/MP" "/sdl-" )
	list( APPEND FO_BUILD_DEFINES "WINRT" "UNICODE" "_UNICODE" "_CRT_SECURE_NO_WARNINGS" "_CRT_SECURE_NO_DEPRECATE" "_WINSOCK_DEPRECATED_NO_WARNINGS" )
	list( APPEND FO_COMMON_SYSTEM_LIBS "user32" "ws2_32" "version" "winmm" "imm32" "dbghelp" "psapi" "d3d9" "gdi32" "xinput" )
elseif( ${CMAKE_SYSTEM_NAME} MATCHES "Linux" )
	StatusMessage( "Operating system: Linux" )
	set( FO_OS "Linux" )
	set( FO_BUILD_CONFIGURATION "RelWithDebInfo" )
	if( CMAKE_SIZEOF_VOID_P EQUAL 4 )
		set( FO_CLIENT_POSTFIX "32" )
		set( FO_NON_CLIENT_POSTFIX "32" )
	endif()
	find_package( X11 REQUIRED )
	find_package( OpenGL REQUIRED )
	set( FO_USE_GLEW YES )
	list( APPEND FO_RENDER_SYSTEM_LIBS "GL" )
	list( APPEND FO_BUILD_OPTIONS "-O2" "-g" )
	list( APPEND FO_BUILD_LINKER_OPTIONS "-no-pie" "-rdynamic" )
	# Todo: rework and use only libc++
	if( FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE OR FONLINE_BUILD_BAKER )
		list( APPEND FO_BUILD_OPTIONS "-stdlib=libstdc++" )
	else()
		list( APPEND FO_BUILD_OPTIONS "-stdlib=libc++" )
	endif()
elseif( APPLE AND NOT PLATFORM )
	StatusMessage( "Operating system: macOS" )
	set( FO_OS "Mac" )
	set( FO_BUILD_CONFIGURATION "RelWithDebInfo" )
	find_package( OpenGL REQUIRED )
	set( FO_USE_GLEW YES )
	list( APPEND FO_RENDER_SYSTEM_LIBS ${OPENGL_LIBRARIES} )
	list( APPEND FO_BUILD_OPTIONS "-stdlib=libc++" "-O2" "-g" )
	list( APPEND FO_BUILD_LINKER_OPTIONS "-no-pie" "-rdynamic" )
elseif( APPLE AND PLATFORM )
	StatusMessage( "Operating system: iOS" )
	set( FO_OS "iOS" )
	set( FO_BUILD_CONFIGURATION "Release" )
	find_library( OPENGLES OpenGLES )
	find_library( METAL Metal )
	find_library( COREGRAPGHICS CoreGraphics )
	find_library( QUARTZCORE QuartzCore )
	find_library( UIKIT UIKit )
	find_library( AVFOUNDATION AVFoundation )
	find_library( GAMECONTROLLER GameController )
	find_library( COREMOTION CoreMotion )
	list( APPEND FO_RENDER_SYSTEM_LIBS ${OPENGLES} ${METAL} ${COREGRAPGHICS} ${QUARTZCORE} ${UIKIT} ${AVFOUNDATION} ${GAMECONTROLLER} ${COREMOTION} )
	unset( OPENGLES )
	unset( METAL )
	unset( COREGRAPGHICS )
	unset( QUARTZCORE )
	unset( UIKIT )
	unset( AVFOUNDATION )
	unset( GAMECONTROLLER )
	unset( COREMOTION )
	list( APPEND FO_BUILD_OPTIONS "-O3" )
	list( APPEND FO_BUILD_LINKER_OPTIONS "-no-pie" )
elseif( ANDROID )
	StatusMessage( "Operating system: Android" )
	set( FO_OS "Android" )
	set( FO_BUILD_CONFIGURATION "Release" )
	set( FO_BUILD_CLIENT_LIBRARY YES )
	list( APPEND FO_RENDER_SYSTEM_LIBS "GLESv1_CM" "GLESv2" )
	list( APPEND FO_COMMON_SYSTEM_LIBS "android" "log" "atomic" )
	list( APPEND FO_BUILD_OPTIONS "-O3" )
	list( APPEND FO_BUILD_LINKER_OPTIONS "-pie" )
elseif( EMSCRIPTEN )
	StatusMessage( "Operating system: Web" )
	set( FO_OS "Web" )
	set( FO_BUILD_CONFIGURATION "Release" )
	set( CMAKE_EXECUTABLE_SUFFIX ".js" )
	list( APPEND FO_BUILD_OPTIONS "-s TOTAL_MEMORY=268435456" ) # 256 Mb
	list( APPEND FO_BUILD_OPTIONS "-s ERROR_ON_UNDEFINED_SYMBOLS=1" )
	list( APPEND FO_BUILD_OPTIONS "-s ALLOW_MEMORY_GROWTH=1" )
	list( APPEND FO_BUILD_OPTIONS "-s USE_WEBGL2=1" )
	list( APPEND FO_BUILD_OPTIONS "-s FORCE_FILESYSTEM=1" )
	list( APPEND FO_BUILD_OPTIONS "-s NO_DYNAMIC_EXECUTION=1" )
	list( APPEND FO_BUILD_OPTIONS "-s USE_SDL_IMAGE=0" )
	list( APPEND FO_BUILD_OPTIONS "-s USE_SDL_TTF=0" )
	list( APPEND FO_BUILD_OPTIONS "-s USE_SDL_NET=0" )
	list( APPEND FO_BUILD_OPTIONS "-s NO_EXIT_RUNTIME=1" )
	list( APPEND FO_BUILD_OPTIONS "-s EXPORTED_RUNTIME_METHODS=\"['FS_createPath', 'FS_createDataFile']\"" )
	list( APPEND FO_BUILD_OPTIONS "-s EXTRA_EXPORTED_RUNTIME_METHODS=\"['Pointer_stringify', 'intArrayFromString', 'UTF8ToString', 'addRunDependency', 'removeRunDependency', 'stackTrace', 'getMemory']\"" )
	list( APPEND FO_BUILD_OPTIONS "--js-library \"${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/mono/repo/sdks/wasm/library_mono.js\"" )
	# list( APPEND FO_BUILD_OPTIONS "-s LLD_REPORT_UNDEFINED=1" )
	if( FONLINE_WEB_DEBUG )
		set( FO_BUILD_CONFIGURATION "Debug" )
		set( FO_CLIENT_POSTFIX "${FO_CLIENT_POSTFIX}_Debug" )
		set( FO_NON_CLIENT_POSTFIX "${FO_NON_CLIENT_POSTFIX}_Debug" )
		list( APPEND FO_BUILD_OPTIONS "-O0" )
		list( APPEND FO_BUILD_OPTIONS "-g3" )
		list( APPEND FO_BUILD_OPTIONS "--memory-init-file 1" )
		list( APPEND FO_BUILD_OPTIONS "-s WASM=0" )
		list( APPEND FO_BUILD_OPTIONS "-s SAFE_HEAP=1" )
		list( APPEND FO_BUILD_OPTIONS "-s ASSERTIONS=1" )
		list( APPEND FO_BUILD_OPTIONS "-s STACK_OVERFLOW_CHECK=1" )
		list( APPEND FO_BUILD_OPTIONS "-s GL_DEBUG=1" )
		list( APPEND FO_BUILD_OPTIONS "-s GL_ASSERTIONS=1" )
		list( APPEND FO_BUILD_OPTIONS "-s GL_TESTING=1" )
		list( APPEND FO_BUILD_OPTIONS "-s ERROR_ON_MISSING_LIBRARIES=1" )
		list( APPEND FO_BUILD_OPTIONS "-s DEMANGLE_SUPPORT=1" )
		list( APPEND FO_BUILD_OPTIONS "-s EXCEPTION_DEBUG=1" )
		list( APPEND FO_BUILD_OPTIONS "-s PRECISE_F32=1" )
		# list( APPEND FO_BUILD_OPTIONS "-s WARN_UNALIGNED=1" )
	else()
		list( APPEND FO_BUILD_OPTIONS "-Oz" )
		list( APPEND FO_BUILD_OPTIONS "--no-heap-copy" )
		# Todo: need?
		# list( APPEND FO_BUILD_OPTIONS "-s WASM=2" )
		# list( APPEND FO_BUILD_OPTIONS "-s DISABLE_EXCEPTION_CATCHING=0" )
		# Todo: uncomment these optimizations later, they may give side effects
		# list( APPEND FO_BUILD_OPTIONS "--closure 1" ) - conflicts with NO_DYNAMIC_EXECUTION
		# list( APPEND FO_BUILD_OPTIONS "-s CLOSURE_WARNINGS=error" )
		# list( APPEND FO_BUILD_OPTIONS "--llvm-lto 1" )
		# list( APPEND FO_BUILD_OPTIONS "-s INLINING_LIMIT=1" )
	endif()
else()
	AbortMessage( "Unknown OS!" )
endif()

string( TOUPPER "${FO_OS}" FO_OS_UPPER )
list( APPEND FO_BUILD_DEFINES "FO_${FO_OS_UPPER}" )

StatusMessage( "Configuration: ${FO_BUILD_CONFIGURATION}" )
set( CMAKE_CONFIGURATION_TYPES "${FO_BUILD_CONFIGURATION}" CACHE STRING "Forced by FOnline" FORCE )
set( CMAKE_BUILD_TYPE "${FO_BUILD_CONFIGURATION}" CACHE STRING "Forced by FOnline" FORCE )

StatusMessage( "Binaries output path: ${FONLINE_OUTPUT_BINARIES_PATH}" )
set( FO_OUTPUT_BINARIES_PATH "${FONLINE_OUTPUT_BINARIES_PATH}" )
file( MAKE_DIRECTORY "${FO_OUTPUT_BINARIES_PATH}" )
if( ANDROID )
	set( FO_CLIENT_OUTPUT "${FO_OUTPUT_BINARIES_PATH}/Client/${FO_OS}/${ANDROID_ABI}" )
else()
	set( FO_CLIENT_OUTPUT "${FO_OUTPUT_BINARIES_PATH}/Client/${FO_OS}" )
endif()
set( FO_SERVER_OUTPUT "${FO_OUTPUT_BINARIES_PATH}/Server" )
set( FO_TOOLS_OUTPUT "${FO_OUTPUT_BINARIES_PATH}/Tools" )
set( FO_TESTS_OUTPUT "${FO_OUTPUT_BINARIES_PATH}/Tests" )

if( FONLINE_NATIVE_SCRIPTING )
	list( APPEND FO_BUILD_DEFINES "FO_NATIVE_SCRIPTING" )
endif()
if( FONLINE_ANGELSCRIPT_SCRIPTING )
	list( APPEND FO_BUILD_DEFINES "FO_ANGELSCRIPT_SCRIPTING" )
endif()
if( FONLINE_MONO_SCRIPTING )
	list( APPEND FO_BUILD_DEFINES "FO_MONO_SCRIPTING" )
endif()

# Compiler options
set( FO_BUILD_OPTIONS "${FO_BUILD_OPTIONS}" )
string( REPLACE ";" " " FO_BUILD_OPTIONS "${FO_BUILD_OPTIONS}" )
string( STRIP "${FO_BUILD_OPTIONS}" FO_BUILD_OPTIONS )
if( MSVC )
	set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /std:c++17 ${FO_BUILD_OPTIONS}" )
else()
	set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++17 ${FO_BUILD_OPTIONS}" )
endif()
string( STRIP "${CMAKE_CXX_FLAGS}" CMAKE_CXX_FLAGS )
StatusMessage( "Compiler flags: ${CMAKE_CXX_FLAGS}" )
set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${FO_BUILD_OPTIONS}" )
if( MSVC )
	set( CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_CXX_FLAGS_MINSIZEREL "${CMAKE_CXX_FLAGS_MINSIZEREL} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_C_FLAGS_MINSIZEREL "${CMAKE_C_FLAGS_MINSIZEREL} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_C_FLAGS_RELWITHDEBINFO "${CMAKE_C_FLAGS_RELWITHDEBINFO} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${FO_BUILD_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${FO_BUILD_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} ${FO_BUILD_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_MINSIZEREL "${CMAKE_EXE_LINKER_FLAGS_MINSIZEREL} ${FO_BUILD_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO} ${FO_BUILD_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_RELEASE "${CMAKE_EXE_LINKER_FLAGS_RELEASE} ${FO_BUILD_LINKER_OPTIONS}" )
else()
	set( CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${FO_BUILD_OPTIONS} ${FO_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} ${FO_BUILD_OPTIONS} ${FO_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_MINSIZEREL "${CMAKE_EXE_LINKER_FLAGS_MINSIZEREL} ${FO_BUILD_OPTIONS} ${FO_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO} ${FO_BUILD_OPTIONS} ${FO_LINKER_OPTIONS}" )
	set( CMAKE_EXE_LINKER_FLAGS_RELEASE "${CMAKE_EXE_LINKER_FLAGS_RELEASE} ${FO_BUILD_OPTIONS} ${FO_LINKER_OPTIONS}" )
endif()
set( CMAKE_CXX_STANDARD 17 )

# Basic includes
include_directories( "Source/Common" )
include_directories( "Source/Server" )
include_directories( "Source/Client" )
include_directories( "Source/Tools" )
include_directories( "Source/Scripting" )

# Generated includes
function( GenerateInclude includeName )
	set( varName GeneratedInclude_${includeName} )
	set( ${varName} "// Automatically generated, do not change manually\n" PARENT_SCOPE )
	set( FO_GENERATED_INCLUDES "${FO_GENERATED_INCLUDES};${includeName}" PARENT_SCOPE )
endfunction()
function( AddGeneratedIncludeLine includeName line )
	set( varName GeneratedInclude_${includeName} )
	set( ${varName} "${${varName}}${line}\n" PARENT_SCOPE )
endfunction()
function( FinalizeGeneratedIncludes )
	foreach( includeName ${FO_GENERATED_INCLUDES} )
		set( varName GeneratedInclude_${includeName} )
		set( filePath "${CMAKE_BINARY_DIR}/GeneratedIncludes/${includeName}.h" )
		set( curContent "" )
		if( EXISTS "${filePath}" )
			file( READ "${filePath}" curContent )
		endif()
		if( NOT "${curContent}" STREQUAL "${${varName}}" )
			file( WRITE "${filePath}" "${${varName}}" )
		endif()
	endforeach()
endfunction()
include_directories( "${CMAKE_BINARY_DIR}/GeneratedIncludes" )

# Embedded resources
GenerateInclude( "EmbeddedResources_Include" )
file( GLOB bins "Resources/Embedded/*.zipped" )
foreach( bin ${bins} )
	string( REGEX MATCH "([^/]+)$" filename ${bin} )
	string( REGEX REPLACE "\\.| " "_" filename ${filename} )
	file( READ ${bin} filedata HEX )
	string( REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1, " filedata ${filedata} )
	AddGeneratedIncludeLine( "EmbeddedResources_Include" "const unsigned char Resource_${filename}[] = { ${filedata}};" )
endforeach()

# Unique hash for build
if( FONLINE_CUSTOM_VERSION STREQUAL "" )
	execute_process( COMMAND git rev-parse --short=7 HEAD WORKING_DIRECTORY "${CMAKE_HOME_DIRECTORY}" RESULT_VARIABLE FO_GIT_HASH_RESULT OUTPUT_VARIABLE FO_GIT_HASH )
	execute_process( COMMAND git diff-index --quiet HEAD WORKING_DIRECTORY "${CMAKE_HOME_DIRECTORY}" RESULT_VARIABLE FO_GIT_DIFF_RESULT )
	string( STRIP "${FO_GIT_HASH}" FO_GIT_HASH )
	if( NOT FO_GIT_HASH_RESULT STREQUAL "0" OR NOT FO_GIT_DIFF_RESULT STREQUAL "0" )
		string( RANDOM LENGTH 5 ALPHABET "0123456789abcdef" SOME_RANDOM )
		set( FONLINE_CUSTOM_VERSION "fff${SOME_RANDOM}" CACHE STRING "Custom version for build (integer FO_VERSION in code), use hex digits (0-9, a-f), maximum are 8" FORCE )
	endif()
endif()

if( FONLINE_CUSTOM_VERSION STREQUAL "" )
	if( $ENV{GITHUB_SHA} )
		StatusMessage( "Build version: ${FO_GIT_HASH} (commit hash at GitHub)" )
		set( FO_VERSION_CONTENT "const int FO_VERSION = 0x${FO_GIT_HASH}; /* Commit hash at GitHub */\nconst bool FO_GITHUB = true;" )
	else()
		StatusMessage( "Build version: ${FO_GIT_HASH} (commit hash)" )
		set( FO_VERSION_CONTENT "const int FO_VERSION = 0x${FO_GIT_HASH}; /* Commit hash */\nconst bool FO_GITHUB = false;" )
	endif()
else()
	StatusMessage( "Build version: ${FONLINE_CUSTOM_VERSION} (custom version)" )
	set( FO_VERSION_CONTENT "const int FO_VERSION = 0x${FONLINE_CUSTOM_VERSION}; /* Custom version */\nconst bool FO_GITHUB = false;" )
endif()

GenerateInclude( "Version_Include" )
AddGeneratedIncludeLine( "Version_Include" "${FO_VERSION_CONTENT}" )

# Third-party libs
StatusMessage( "Third-party libs:" )

function( DisableLibWarnings )
	foreach( lib ${ARGV} )
		target_compile_options( ${lib} PRIVATE
			$<$<OR:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_ID:AppleClang>,$<CXX_COMPILER_ID:GNU>>:-w>
			$<$<CXX_COMPILER_ID:MSVC>:/W0> )
	endforeach()
endfunction()

# SDL2
StatusMessage( "+ SDL2" )
set( FO_SDL_DIR "ThirdParty/SDL2" )
set( SDL_SHARED OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
set( RENDER_D3D OFF CACHE BOOL "Forced by FOnline" FORCE )
set( LIBC ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_AUDIO ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_VIDEO ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_RENDER ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_EVENTS ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_LOADSO ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_ATOMIC OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_JOYSTICK OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_HAPTIC OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_POWER ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_THREADS ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_TIMERS ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_FILE ON CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_CPUINFO OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_FILESYSTEM OFF CACHE BOOL "Forced by FOnline" FORCE )
set( SDL_DLOPEN ON CACHE BOOL "Forced by FOnline" FORCE )
if( ANDROID )
	set( SDL_JOYSTICK ON CACHE BOOL "Forced by FOnline" FORCE )
	set( PTHREADS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( HIDAPI OFF CACHE BOOL "Forced by FOnline" FORCE )
endif()
if( APPLE AND PLATFORM )
	set( SDL_JOYSTICK ON CACHE BOOL "Forced by FOnline" FORCE )
	set( SDL_HAPTIC ON CACHE BOOL "Forced by FOnline" FORCE )
endif()
if( EMSCRIPTEN )
	set( SDL_JOYSTICK ON CACHE BOOL "Forced by FOnline" FORCE )
endif()
if( WIN32 AND WINRT )
	include( "BuildTools/sdl-winrt.cmake" )
	add_library( SDL2-static STATIC ${FO_SDL_WINRT_SOURCE} ${FO_SDL_WINRT_CX_SOURCE} )
	add_library( SDL2main STATIC "${FO_SDL_DIR}/src/main/winrt/SDL_winrt_main_NonXAML.cpp" )
else()
	add_subdirectory( "${FO_SDL_DIR}" )
endif()
include_directories( "${FO_SDL_DIR}/include" )
list( APPEND FO_BUILD_DEFINES "GL_GLEXT_PROTOTYPES" )
target_compile_definitions( SDL2main PRIVATE "GL_GLEXT_PROTOTYPES" )
target_compile_definitions( SDL2-static PRIVATE "GL_GLEXT_PROTOTYPES" )
list( APPEND FO_RENDER_LIBS "SDL2main" "SDL2-static" )
DisableLibWarnings( SDL2main SDL2-static )

# Zlib
StatusMessage( "+ Zlib" )
set( FO_ZLIB_DIR "ThirdParty/zlib" )
add_subdirectory( "${FO_ZLIB_DIR}" )
include_directories( "${FO_ZLIB_DIR}" "${FO_ZLIB_DIR}/contrib" "${CMAKE_BINARY_DIR}/${FO_ZLIB_DIR}" )
list( APPEND FO_COMMON_LIBS "zlibstatic" )
DisableLibWarnings( zlibstatic )

# PNG
StatusMessage( "+ PNG" )
if( NOT ( ANDROID OR ( APPLE AND PLATFORM ) OR EMSCRIPTEN ) )
	set( FO_PNG_DIR "ThirdParty/PNG" )
	set( SKIP_INSTALL_ALL ON CACHE BOOL "Forced by FOnline" FORCE )
	set( ZLIB_LIBRARY "zlibstatic" CACHE STRING "Forced by FOnline" FORCE )
	set( ZLIB_INCLUDE_DIR "../${FO_ZLIB_DIR}" "${CMAKE_BINARY_DIR}/${FO_ZLIB_DIR}" CACHE STRING "Forced by FOnline" FORCE )
	set( PNG_SHARED OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( PNG_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_PNG_DIR}" )
	include_directories( "${FO_PNG_DIR}" "${CMAKE_BINARY_DIR}/${FO_PNG_DIR}" )
	list( APPEND FO_COMMON_LIBS "png16_static" )
	DisableLibWarnings( png16_static )
endif()

# Ogg & Vorbis & Theora & Acm
# Todo: FONLINE_BUILD_BAKER
if( YES )
	# Ogg
	StatusMessage( "+ Ogg" )
	set( FO_OGG_DIR "ThirdParty/ogg" )
	file( GLOB FO_OGG_SOURCE "${FO_OGG_DIR}/src/*.c" )
	include_directories( "${FO_OGG_DIR}/include" )
	add_library( Ogg ${FO_OGG_SOURCE} )
	list( APPEND FO_CLIENT_LIBS "Ogg" )
	DisableLibWarnings( Ogg )

	# Vorbis
	StatusMessage( "+ Vorbis" )
	set( FO_VORBIS_DIR "ThirdParty/Vorbis" )
	file( GLOB FO_VORBIS_SOURCE "${FO_VORBIS_DIR}/lib/*.c" )
	list( REMOVE_ITEM FO_VORBIS_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_VORBIS_DIR}/lib/barkmel.c" )
	list( REMOVE_ITEM FO_VORBIS_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_VORBIS_DIR}/lib/psytune.c" )
	list( REMOVE_ITEM FO_VORBIS_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_VORBIS_DIR}/lib/tone.c" )
	include_directories( "${FO_VORBIS_DIR}/include" )
	include_directories( "${FO_VORBIS_DIR}/lib" )
	add_library( Vorbis ${FO_VORBIS_SOURCE} )
	target_link_libraries( Vorbis Ogg )
	list( APPEND FO_CLIENT_LIBS "Vorbis" )
	target_compile_definitions( Vorbis PRIVATE "_CRT_SECURE_NO_WARNINGS" )
	DisableLibWarnings( Vorbis )

	# Theora
	StatusMessage( "+ Theora" )
	set( FO_THEORA_DIR "ThirdParty/Theora" )
	file( GLOB FO_THEORA_SOURCE "${FO_THEORA_DIR}/lib/*.c" )
	list( REMOVE_ITEM FO_THEORA_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_THEORA_DIR}/lib/analyze.c" )
	list( REMOVE_ITEM FO_THEORA_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_THEORA_DIR}/lib/encode.c" )
	list( REMOVE_ITEM FO_THEORA_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_THEORA_DIR}/lib/encapiwrapper.c" )
	include_directories( "${FO_THEORA_DIR}/include" )
	add_library( Theora ${FO_THEORA_SOURCE} )
	list( APPEND FO_CLIENT_LIBS "Theora" )
	DisableLibWarnings( Theora )

	# Acm
	StatusMessage( "+ Acm" )
	set( FO_ACM_DIR "ThirdParty/Acm" )
	add_subdirectory( "${FO_ACM_DIR}" )
	include_directories( "${FO_ACM_DIR}" )
	list( APPEND FO_CLIENT_LIBS "Acm" )
	DisableLibWarnings( Acm )
endif()

# SHA
StatusMessage( "+ SHA" )
set( FO_SHA_DIR "ThirdParty/SHA" )
add_subdirectory( "${FO_SHA_DIR}" )
include_directories( "${FO_SHA_DIR}" )
list( APPEND FO_COMMON_LIBS "SHA" )
DisableLibWarnings( SHA )

# GLEW
if( FO_USE_GLEW )
	StatusMessage( "+ GLEW" )
	set( FO_GLEW_DIR "ThirdParty/GLEW" )
	file( GLOB FO_GLEW_SOURCE "${FO_GLEW_DIR}/GL/*.*" )
	include_directories( "${FO_GLEW_DIR}" )
	add_library( GLEW ${FO_GLEW_SOURCE} )
	list( APPEND FO_BUILD_DEFINES "GLEW_STATIC" )
	target_compile_definitions( GLEW PRIVATE "GLEW_STATIC" )
	list( APPEND FO_RENDER_LIBS "GLEW" )
	DisableLibWarnings( GLEW )
endif()

# NCodeHook
if( WIN32 )
	StatusMessage( "+ NCodeHook" )
	set( FO_NCODE_HOOK_DIR "ThirdParty/NCodeHook" )
	add_subdirectory( "${FO_NCODE_HOOK_DIR}" )
	include_directories( "${FO_NCODE_HOOK_DIR}" )
	list( APPEND FO_COMMON_LIBS "NCodeHook" )
	DisableLibWarnings( NCodeHook )
endif()

# Assimp
set( FO_ASSIMP_DIR "ThirdParty/Assimp" )
if( FONLINE_BUILD_BAKER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ Assimp" )
	set( ASSIMP_NO_EXPORT ON CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_BUILD_ASSIMP_TOOLS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_BUILD_ASSIMP_VIEW OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_BUILD_SAMPLES OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_BUILD_TESTS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_OPT_BUILD_PACKAGES OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ASSIMP_INSTALL_PDB OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( IGNORE_GIT_HASH ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_ASSIMP_DIR}" )
	include_directories( "${CMAKE_BINARY_DIR}/${FO_ASSIMP_DIR}/include" )
	list( APPEND FO_BAKER_LIBS "assimp" "IrrXML" )
	DisableLibWarnings( assimp IrrXML )
else()
	StatusMessage( "+ Assimp headers" )
endif()
include_directories( "${FO_ASSIMP_DIR}/include" )

# Fbx SDK
if( FONLINE_BUILD_BAKER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	if( NOT CMAKE_SIZEOF_VOID_P EQUAL 8 )
		message( FATAL_ERROR "Using of FBX SDK for non 64 bit builds is not supported" )
	endif()
	if( NOT WIN32 AND NOT ${CMAKE_SYSTEM_NAME} MATCHES "Linux" )
		message( FATAL_ERROR "Using of FBX SDK for non Windows or Linux builds is not supported" )
	endif()

	StatusMessage( "+ Fbx SDK" )
	set( FO_FBXSDK_DIR "ThirdParty/fbxsdk" )
	include_directories( "${FO_FBXSDK_DIR}" )
	if( WIN32 )
		file( COPY "${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/fbxsdk/libfbxsdk.dll" DESTINATION "${FO_TOOLS_OUTPUT}" )
		list( APPEND FO_BAKER_SYSTEM_LIBS "${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/fbxsdk/libfbxsdk.lib" )
	else()
		file( COPY "${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/fbxsdk/libfbxsdk.so" DESTINATION "${FO_TOOLS_OUTPUT}" )
		list( APPEND FO_BAKER_SYSTEM_LIBS "${CMAKE_CURRENT_SOURCE_DIR}/ThirdParty/fbxsdk/libfbxsdk.so" )
	endif()
endif()

# Nlohmann Json
StatusMessage( "+ Nlohmann Json" )
set( FO_JSON_DIR "ThirdParty/Json" )
include_directories( "${FO_JSON_DIR}" )

# Fmt
StatusMessage( "+ Fmt" )
set( FO_FMT_DIR "ThirdParty/fmt" )
add_subdirectory( "${FO_FMT_DIR}" )
include_directories( "${FO_FMT_DIR}/include" )
list( APPEND FO_COMMON_LIBS "fmt" )
DisableLibWarnings( fmt )

# OpenSSL
if( FONLINE_BUILD_SERVER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ OpenSSL" )
	set( FO_OPENSSL_DIR "ThirdParty/openssl" )
	set( BUILD_OBJECT_LIBRARY_ONLY ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_OPENSSL_DIR}" )
	include_directories( "${FO_OPENSSL_DIR}" )
	include_directories( "${CMAKE_BINARY_DIR}/${FO_OPENSSL_DIR}/crypto" )
	include_directories( "${CMAKE_BINARY_DIR}/${FO_OPENSSL_DIR}/ssl" )
	list( APPEND FO_SERVER_LIBS "ssl" "crypto" )
	DisableLibWarnings( ssl crypto )
endif()

# Asio & Websockets
if( FONLINE_BUILD_SERVER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ Asio" )
	set( FO_ASIO_DIR "ThirdParty/Asio" )
	include_directories( "${FO_ASIO_DIR}/include" )

	StatusMessage( "+ Websockets" )
	set( FO_WEBSOCKETS_DIR "ThirdParty/websocketpp" )
	include_directories( "${FO_WEBSOCKETS_DIR}" )
endif()

# MongoDB & Bson
if( FONLINE_BUILD_SERVER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ Bson" )
	set( FO_BSON_DIR "ThirdParty/libbson" )
	set( ENABLE_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
	set( BSON_ENABLE_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_BSON_DIR}" )
	set( BSON_INCLUDE_DIR "${CMAKE_BINARY_DIR}/${FO_BSON_DIR}/src/bson"
		"${CMAKE_CURRENT_SOURCE_DIR}/${FO_BSON_DIR}/src/bson" CACHE STRING "Forced by FOnline" FORCE )
	set( BSON_LIBRARIES "bson_static" CACHE STRING "Forced by FOnline" FORCE )
	set( BSON_STATIC_LIBRARIES "bson_static" CACHE STRING "Forced by FOnline" FORCE )
	include_directories( "${BSON_INCLUDE_DIR}" )
	target_compile_definitions( bson_static PRIVATE "BSON_COMPILATION;BSON_STATIC;JSONSL_PARSE_NAN" )
	list( APPEND FO_BUILD_DEFINES "BSON_COMPILATION" "BSON_STATIC" "JSONSL_PARSE_NAN" )
	list( APPEND FO_SERVER_LIBS "bson_static" )
	DisableLibWarnings( bson_static )

	StatusMessage( "+ MongoDB" )
	set( FO_MONGODB_DIR "ThirdParty/mongo-c-driver" )
	set( ENABLE_SASL OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( MONGOC_ENABLE_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_MONGODB_DIR}" )
	set( MONGOC_INCLUDE_DIR "${CMAKE_BINARY_DIR}/${FO_MONGODB_DIR}/src/mongoc"
		"${CMAKE_CURRENT_SOURCE_DIR}/${FO_MONGODB_DIR}/src/mongoc" CACHE STRING "Forced by FOnline" FORCE )
	include_directories( "${MONGOC_INCLUDE_DIR}" )
	target_compile_definitions( mongoc_static PRIVATE "BSON_COMPILATION;BSON_STATIC;JSONSL_PARSE_NAN" )
	list( APPEND FO_SERVER_LIBS "mongoc_static" )
	DisableLibWarnings( mongoc_static )
endif()

# Unqlite
if( NOT EMSCRIPTEN )
	StatusMessage( "+ Unqlite" )
	set( FO_UNQLITE_DIR "ThirdParty/unqlite" )
	add_subdirectory( "${FO_UNQLITE_DIR}" )
	include_directories( "${FO_UNQLITE_DIR}" )
	list( APPEND FO_COMMON_LIBS "unqlite" )
	DisableLibWarnings( unqlite )
endif()

# cURL
if( FONLINE_BUILD_SERVER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ cURL" )
	set( FO_CURL_DIR "ThirdParty/cURL" )
	set( HAVE_POLL_FINE_EXITCODE 0 CACHE STRING "Forced by FOnline" FORCE )
	set( CURL_STATICLIB ON CACHE BOOL "Forced by FOnline" FORCE )
	set( CURL_CA_PATH "none" CACHE STRING "Forced by FOnline" FORCE )
	set( BUILD_CURL_EXE OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_MANUAL OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( CMAKE_USE_OPENSSL OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( OPENSSL_INCLUDE_DIR "${CMAKE_BINARY_DIR}/${FO_OPENSSL_DIR}/ssl" CACHE STRING "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_CURL_DIR}" )
	include_directories( "${FO_CURL_DIR}/include" )
	target_compile_definitions( libcurl PRIVATE "CURL_STATICLIB" )
	list( APPEND FO_BUILD_DEFINES "CURL_STATICLIB" )
	list( APPEND FO_SERVER_LIBS "libcurl" )
	DisableLibWarnings( libcurl )
endif()

# Dear ImGui
StatusMessage( "+ Dear ImGui" )
set( FO_DEAR_IMGUI_DIR "ThirdParty/imgui" )
file( GLOB FO_IMGUI_SOURCE "${FO_DEAR_IMGUI_DIR}/*.cpp" "${FO_DEAR_IMGUI_DIR}/*.h" )
#list( REMOVE_ITEM FO_IMGUI_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_DEAR_IMGUI_DIR}/imgui_demo.cpp" )
#list( REMOVE_ITEM FO_IMGUI_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/${FO_DEAR_IMGUI_DIR}/imgui_demo.h" )
include_directories( "${FO_DEAR_IMGUI_DIR}" )
add_library( ImGui ${FO_IMGUI_SOURCE} )
target_compile_definitions( ImGui PRIVATE "IMGUI_DISABLE_OBSOLETE_FUNCTIONS" )
list( APPEND FO_BUILD_DEFINES "IMGUI_DISABLE_OBSOLETE_FUNCTIONS" )
list( APPEND FO_COMMON_LIBS "ImGui" )
if( WIN32 AND WINRT )
	target_compile_definitions( ImGui PRIVATE "IMGUI_DISABLE_WIN32_DEFAULT_CLIPBOARD_FUNCTIONS" )
	list( APPEND FO_BUILD_DEFINES "IMGUI_DISABLE_WIN32_DEFAULT_CLIPBOARD_FUNCTIONS" )
endif()
DisableLibWarnings( ImGui )

# Catch2
StatusMessage( "+ Catch2" )
set( FO_CATCH2_DIR "ThirdParty/Catch2" )
include_directories( "${FO_CATCH2_DIR}/single_include/catch2" )

# Backward-cpp
if( NOT WIN32 )
	StatusMessage( "+ Backward-cpp" )
	set( FO_BACKWARDCPP_DIR "ThirdParty/backward-cpp" )
	include_directories( "${FO_BACKWARDCPP_DIR}" )
	if( FO_OS STREQUAL "Linux" )
		list( APPEND FO_COMMON_SYSTEM_LIBS "bfd" )
	endif()
endif()

# Spark
StatusMessage( "+ Spark" )
set( FO_SPARK_DIR "ThirdParty/spark" )
set( SPARK_STATIC_BUILD ON CACHE BOOL "Forced by FOnline" FORCE )
add_subdirectory( "${FO_SPARK_DIR}/projects/engine/core" )
include_directories( "${CMAKE_BINARY_DIR}/${FO_SPARK_DIR}/include" )
list( APPEND FO_CLIENT_LIBS "SPARK_Core" )
DisableLibWarnings( SPARK_Core )

# glslang & SPIRV-Cross
if( FONLINE_BUILD_BAKER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	StatusMessage( "+ glslang" )
	set( FO_GLSLANG_DIR "ThirdParty/glslang" )
	set( BUILD_EXTERNAL OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SKIP_GLSLANG_INSTALL ON CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_HLSL "OFF" CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_GLSLANG_BINARIES OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_SPVREMAPPER OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_AMD_EXTENSIONS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_NV_EXTENSIONS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_CTEST OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_OPT OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_PCH OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( ENABLE_CTEST OFF CACHE BOOL "Forced by FOnline" FORCE )
	if( EMSCRIPTEN )
		set( ENABLE_GLSLANG_WEB ON CACHE BOOL "Forced by FOnline" FORCE )
		set( ENABLE_GLSLANG_WEB_DEVEL ON CACHE BOOL "Forced by FOnline" FORCE )
		set( ENABLE_EMSCRIPTEN_SINGLE_FILE ON CACHE BOOL "Forced by FOnline" FORCE )
		set( ENABLE_EMSCRIPTEN_ENVIRONMENT_NODE OFF CACHE BOOL "Forced by FOnline" FORCE )
	endif()
	add_subdirectory( "${FO_GLSLANG_DIR}" )
	include_directories( "${FO_GLSLANG_DIR}/glslang/Public" )
	include_directories( "${FO_GLSLANG_DIR}/SPIRV" )
	list( APPEND FO_BAKER_LIBS "glslang" "OGLCompiler" "OSDependent" "SPIRV" )
	DisableLibWarnings( glslang OGLCompiler OSDependent SPIRV )

	StatusMessage( "+ SPIRV-Cross" )
	set( FO_SPIRV_CROSS_DIR "ThirdParty/SPIRV-Cross" )
	set( SPIRV_CROSS_EXCEPTIONS_TO_ASSERTIONS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_STATIC ON CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_SHARED OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_CLI OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_TESTS OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_GLSL ON CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_HLSL ON CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_MSL ON CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_CPP OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_REFLECT OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_C_API OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_CROSS_ENABLE_UTIL OFF CACHE BOOL "Forced by FOnline" FORCE )
	set( SPIRV_SKIP_TESTS ON CACHE BOOL "Forced by FOnline" FORCE )
	add_subdirectory( "${FO_SPIRV_CROSS_DIR}" )
	include_directories( "${FO_SPIRV_CROSS_DIR}" )
	include_directories( "${FO_SPIRV_CROSS_DIR}/include" )
	list( APPEND FO_BAKER_LIBS "spirv-cross-core" "spirv-cross-glsl" "spirv-cross-hlsl" "spirv-cross-msl" )
	DisableLibWarnings( spirv-cross-core spirv-cross-glsl spirv-cross-hlsl spirv-cross-msl )
endif()

# AngelScript scripting
if( FONLINE_ANGELSCRIPT_SCRIPTING )
	# AngelScript
	StatusMessage( "+ AngelScript" )
	set( FO_ANGELSCRIPT_DIR "ThirdParty/AngelScript" )
	add_subdirectory( "${FO_ANGELSCRIPT_DIR}/sdk/angelscript/projects/cmake" )
	include_directories( "${FO_ANGELSCRIPT_DIR}/sdk/angelscript/include" "${FO_ANGELSCRIPT_DIR}/sdk/angelscript/source" "${FO_ANGELSCRIPT_DIR}/sdk/add_on" )
	list( APPEND FO_COMMON_LIBS "Angelscript" )
	DisableLibWarnings( Angelscript )

	# AngelScriptExt
	StatusMessage( "+ AngelScriptExt" )
	set( FO_ANGELSCRIPT_EXT_DIR "Source/Common/AngelScriptExt" )
	set( FO_ANGELSCRIPT_PREPROCESSOR_DIR "ThirdParty/AngelScript/preprocessor" )
	set( FO_ANGELSCRIPT_SDK_DIR "ThirdParty/AngelScript/sdk" )
	include_directories( "${FO_ANGELSCRIPT_EXT_DIR}" )
	include_directories( "${FO_ANGELSCRIPT_PREPROCESSOR_DIR}" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptstdstring" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptarray" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptdictionary" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptfile" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptany" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/datetime" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptmath" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/weakref" )
	include_directories( "${FO_ANGELSCRIPT_SDK_DIR}/add_on/scripthelper" )
	file( GLOB FO_ANGELSCRIPT_EXT_SOURCE
		"${FO_ANGELSCRIPT_EXT_DIR}/*.*"
		"${FO_ANGELSCRIPT_PREPROCESSOR_DIR}/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptstdstring/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptarray/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptdictionary/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptfile/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptany/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/datetime/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scriptmath/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/weakref/*.*"
		"${FO_ANGELSCRIPT_SDK_DIR}/add_on/scripthelper/*.*" )
	add_library( AngelscriptExt ${FO_ANGELSCRIPT_EXT_SOURCE} )
	target_link_libraries( AngelscriptExt Angelscript )
	list( APPEND FO_COMMON_LIBS "AngelscriptExt" )
	target_compile_definitions( AngelscriptExt PRIVATE "_CRT_SECURE_NO_WARNINGS" "FO_${FO_OS_UPPER}" )
	DisableLibWarnings( AngelscriptExt )

	if( NOT FONLINE_BUILD_BAKER )
		target_compile_definitions( Angelscript PRIVATE "AS_NO_COMPILER" )
		target_compile_definitions( AngelscriptExt PRIVATE "AS_NO_COMPILER" )
		list( APPEND FO_BUILD_DEFINES "AS_NO_COMPILER" )
	endif()
	if( EMSCRIPTEN OR ( APPLE AND PLATFORM ) OR ( ANDROID AND CMAKE_SIZEOF_VOID_P EQUAL 8 ) )
		target_compile_definitions( Angelscript PRIVATE "AS_MAX_PORTABILITY" )
		target_compile_definitions( AngelscriptExt PRIVATE "AS_MAX_PORTABILITY" )
		list( APPEND FO_BUILD_DEFINES "AS_MAX_PORTABILITY" )
	endif()
	if( EMSCRIPTEN )
		target_compile_definitions( Angelscript PRIVATE "WIP_16BYTE_ALIGN" )
		target_compile_definitions( AngelscriptExt PRIVATE "WIP_16BYTE_ALIGN" )
		list( APPEND FO_BUILD_DEFINES "WIP_16BYTE_ALIGN" )
	endif()
endif()

# Mono scripting
if( FONLINE_MONO_SCRIPTING )
	StatusMessage( "+ Mono" )
	set( FO_MONO_DIR "ThirdParty/mono" )
	add_subdirectory( "${FO_MONO_DIR}" )
	include_directories( "${FO_MONO_DIR}" )
	include_directories( "${FO_MONO_DIR}/repo" )
	include_directories( "${FO_MONO_DIR}/repo/mono" )
	include_directories( "${FO_MONO_DIR}/repo/mono/eglib" )
	list( APPEND FO_COMMON_LIBS "libmono" )
	target_compile_definitions( libmono PRIVATE "HAVE_EXTERN_DEFINED_WINAPI_SUPPORT" "_CRT_SECURE_NO_WARNINGS" "_CRT_SECURE_NO_DEPRECATE" "_CRT_NONSTDC_NO_WARNINGS" )
	if( ANDROID_ABI STREQUAL "x86" )
		target_compile_definitions( libmono PRIVATE "ANDROID_X86" )
	endif()
	list( APPEND FO_BUILD_DEFINES "HAVE_EXTERN_DEFINED_WINAPI_SUPPORT" )
	DisableLibWarnings( libmono )
endif()

# Collect sources
file( GLOB FO_COMMON_SOURCE "Source/Common/*.*" )
list( REMOVE_ITEM FO_COMMON_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Common/MessageBox.cpp" )
list( REMOVE_ITEM FO_COMMON_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Common/MessageBoxStub.cpp" )
file( GLOB FO_SERVER_SOURCE "Source/Server/*.*" )
list( REMOVE_ITEM FO_SERVER_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Server/AppGui.cpp" )
list( REMOVE_ITEM FO_SERVER_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Server/AppGuiDX.cpp" )
list( REMOVE_ITEM FO_SERVER_SOURCE "${CMAKE_CURRENT_SOURCE_DIR}/Source/Server/AppGuiStub.cpp" )
file( GLOB FO_CLIENT_SOURCE "Source/Client/*.*" )
file( GLOB FO_TOOLS_SOURCE "Source/Tools/*.*" )
file( GLOB FO_SCRIPTING_SOURCE "Source/Scripting/*.*" )

# Applications
StatusMessage( "Applications:" )

if( FONLINE_BUILD_CLIENT OR FONLINE_BUILD_SERVER OR FONLINE_BUILD_MAPPER OR FONLINE_BUILD_BAKER )
	add_library( CommonLib STATIC ${FO_COMMON_SOURCE} )
	set_target_properties( CommonLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	list( APPEND FO_CORE_LIBS_GROUP "CommonLib" )
endif()
if( FONLINE_BUILD_CLIENT OR FONLINE_BUILD_MAPPER )
	add_library( ClientLib STATIC ${FO_CLIENT_SOURCE} )
	set_target_properties( ClientLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	list( APPEND FO_CORE_LIBS_GROUP "ClientLib" )
endif()
if( FONLINE_BUILD_SERVER )
	add_library( ServerLib STATIC ${FO_SERVER_SOURCE} )
	set_target_properties( ServerLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	list( APPEND FO_CORE_LIBS_GROUP "ServerLib" )
endif()

if( FONLINE_BUILD_CLIENT OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	add_library( ClientScriptingLib STATIC ${FO_SCRIPTING_SOURCE} "${CMAKE_CURRENT_SOURCE_DIR}/Source/Client/ClientScripting.h" )
	set_target_properties( ClientScriptingLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES};FO_CLIENT_SCRIPTING" )
	target_link_libraries( ClientLib "ClientScriptingLib" )
	list( APPEND FO_CORE_LIBS_GROUP "ClientScriptingLib" )
endif()
if( FONLINE_BUILD_SERVER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	add_library( ServerScriptingLib STATIC ${FO_SCRIPTING_SOURCE} "${CMAKE_CURRENT_SOURCE_DIR}/Source/Server/ServerScripting.h" )
	set_target_properties( ServerScriptingLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES};FO_SERVER_SCRIPTING" )
	target_link_libraries( ServerLib "ServerScriptingLib" )
	list( APPEND FO_CORE_LIBS_GROUP "ServerScriptingLib" )
endif()
if( FONLINE_BUILD_MAPPER OR FONLINE_UNIT_TESTS OR FONLINE_CODE_COVERAGE )
	add_library( MapperScriptingLib STATIC ${FO_SCRIPTING_SOURCE} "${CMAKE_CURRENT_SOURCE_DIR}/Source/Tools/MapperScripting.h" )
	set_target_properties( MapperScriptingLib PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES};FO_MAPPER_SCRIPTING" )
	list( APPEND FO_CORE_LIBS_GROUP "MapperScriptingLib" )
endif()

if( FONLINE_BUILD_CLIENT )
	if( NOT FO_BUILD_CLIENT_LIBRARY )
		StatusMessage( "+ FOnline" )
		list( APPEND FO_APPLICATIONS_GROUP "FOnline" )
		add_executable( FOnline WIN32 "Source/Applications/ClientApp.cpp" "Source/Common/MessageBox.cpp" "Resources/Default.rc" )
		# Todo: cmake make bundles for Mac and iOS
		# add_executable( FOnline MACOSX_BUNDLE ... "Default.rc" )
		set_target_properties( FOnline PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_CLIENT_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_CLIENT_OUTPUT}
			RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_CLIENT_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_CLIENT_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_CLIENT_OUTPUT} )
	else()
		StatusMessage( "+ FOnline (shared library)" )
		list( APPEND FO_APPLICATIONS_GROUP "FOnline" )
		add_library( FOnline SHARED "Source/Applications/ClientApp.cpp" "Source/Common/MessageBox.cpp" )
		set_target_properties( FOnline PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${FO_CLIENT_OUTPUT} )
	endif()
	set_target_properties( FOnline PROPERTIES OUTPUT_NAME "FOnline${FO_CLIENT_POSTFIX}" )
	set_target_properties( FOnline PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	target_link_libraries( FOnline "ClientLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_CLIENT_SYSTEM_LIBS}" "${FO_CLIENT_LIBS}" "${FO_RENDER_SYSTEM_LIBS}" "${FO_RENDER_LIBS}" "${CMAKE_DL_LIBS}" )
endif()

if( FONLINE_BUILD_SERVER )
	StatusMessage( "+ FOnlineServer" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineServer" )
	add_executable( FOnlineServer WIN32 "Source/Applications/ServerApp.cpp"  "Source/Server/AppGui.cpp" "Source/Server/AppGuiDX.cpp" "Source/Common/MessageBox.cpp" "Resources/Default.rc" )
	set_target_properties( FOnlineServer PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_SERVER_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_SERVER_OUTPUT} )
	set_target_properties( FOnlineServer PROPERTIES OUTPUT_NAME "FOnlineServer${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineServer PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	target_link_libraries( FOnlineServer "ServerLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${FO_RENDER_SYSTEM_LIBS}" "${FO_RENDER_LIBS}" "${CMAKE_DL_LIBS}" )

	StatusMessage( "+ FOnlineServerHeadless" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineServerHeadless" )
	add_executable( FOnlineServerHeadless "Source/Applications/ServerHeadlessApp.cpp" "Source/Server/AppGuiStub.cpp" "Source/Common/MessageBoxStub.cpp" )
	set_target_properties( FOnlineServerHeadless PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_SERVER_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_SERVER_OUTPUT} )
	set_target_properties( FOnlineServerHeadless PROPERTIES OUTPUT_NAME "FOnlineServerHeadless${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineServerHeadless PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	target_link_libraries( FOnlineServerHeadless "ServerLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${CMAKE_DL_LIBS}" )

	if( WIN32 )
		StatusMessage( "+ FOnlineServerService" )
		list( APPEND FO_APPLICATIONS_GROUP "FOnlineServerService" )
		add_executable( FOnlineServerService "Source/Applications/ServerServiceApp.cpp" "Source/Server/AppGuiStub.cpp" "Source/Common/MessageBoxStub.cpp" )
		set_target_properties( FOnlineServerService PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_SERVER_OUTPUT}
			RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_SERVER_OUTPUT} )
		set_target_properties( FOnlineServerService PROPERTIES OUTPUT_NAME "FOnlineServerService${FO_NON_CLIENT_POSTFIX}" )
		set_target_properties( FOnlineServerService PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
		target_link_libraries( FOnlineServerService "ServerLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${CMAKE_DL_LIBS}" )
	else()
		StatusMessage( "+ FOnlineServerDaemon" )
		list( APPEND FO_APPLICATIONS_GROUP "FOnlineServerDaemon" )
		add_executable( FOnlineServerDaemon "Source/Applications/ServerDaemonApp.cpp" "Source/Server/AppGuiStub.cpp" "Source/Common/MessageBoxStub.cpp" )
		set_target_properties( FOnlineServerDaemon PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_SERVER_OUTPUT}
			RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_SERVER_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_SERVER_OUTPUT} )
		set_target_properties( FOnlineServerDaemon PROPERTIES OUTPUT_NAME "FOnlineServerDaemon${FO_NON_CLIENT_POSTFIX}" )
		set_target_properties( FOnlineServerDaemon PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
		target_link_libraries( FOnlineServerDaemon "ServerLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${CMAKE_DL_LIBS}" )
	endif()
endif()

if( FONLINE_BUILD_MAPPER )
	StatusMessage( "+ FOnlineMapper" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineMapper" )
	add_executable( FOnlineMapper WIN32 "Source/Applications/MapperApp.cpp" "Source/Common/MessageBox.cpp" "Source/Tools/Mapper.h" "Source/Tools/Mapper.cpp" "Resources/Default.rc" "Source/Tools/MapperScripting.h" )
	set_target_properties( FOnlineMapper PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_TOOLS_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_TOOLS_OUTPUT} )
	set_target_properties( FOnlineMapper PROPERTIES OUTPUT_NAME "FOnlineMapper${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineMapper PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	target_link_libraries( FOnlineMapper "MapperScriptingLib" "ClientLib" "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_CLIENT_SYSTEM_LIBS}" "${FO_CLIENT_LIBS}" "${FO_RENDER_SYSTEM_LIBS}" "${FO_RENDER_LIBS}" "${CMAKE_DL_LIBS}" )
endif()

if( FONLINE_BUILD_BAKER )
	StatusMessage( "+ FOnlineBaker" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineBaker" )
	add_executable( FOnlineBaker "Source/Applications/BakerApp.cpp" "Source/Common/MessageBoxStub.cpp" "Source/Tools/ImageBaker.h" "Source/Tools/ImageBaker.cpp" "Source/Tools/ModelBaker.h" "Source/Tools/ModelBaker.cpp" "Source/Tools/EffectBaker.h" "Source/Tools/EffectBaker.cpp" "Source/Tools/F2Palette_Include.h" )
	set_target_properties( FOnlineBaker PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_TOOLS_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_TOOLS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_TOOLS_OUTPUT} )
	set_target_properties( FOnlineBaker PROPERTIES OUTPUT_NAME "FOnlineBaker${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineBaker PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES}" )
	target_link_libraries( FOnlineBaker "CommonLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_BAKER_SYSTEM_LIBS}" "${FO_BAKER_LIBS}" "${CMAKE_DL_LIBS}" )
endif()

if( FONLINE_UNIT_TESTS )
	StatusMessage( "+ FOnlineUnitTests" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineUnitTests" )
	add_executable( FOnlineUnitTests ${FO_TOOLS_SOURCE} ${FO_SERVER_SOURCE} "Source/Server/AppGuiStub.cpp" ${FO_CLIENT_SOURCE} ${FO_COMMON_SOURCE} "Source/Common/MessageBox.cpp" "Source/Applications/ServerApp.cpp" "Source/Applications/ServerServiceApp.cpp" "Source/Applications/ServerDaemonApp.cpp" "Source/Applications/ServerHeadlessApp.cpp" "Source/Applications/ClientApp.cpp" "Source/Applications/MapperApp.cpp" "Source/Applications/BakerApp.cpp" )
	set_target_properties( FOnlineUnitTests PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_TESTS_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_TESTS_OUTPUT} )
	set_target_properties( FOnlineUnitTests PROPERTIES OUTPUT_NAME "FOnlineUnitTests${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineUnitTests PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES};FO_TESTING" )
	target_link_libraries( FOnlineUnitTests "ServerScriptingLib" "ClientScriptingLib" "MapperScriptingLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_BAKER_SYSTEM_LIBS}" "${FO_BAKER_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${FO_CLIENT_SYSTEM_LIBS}" "${FO_CLIENT_LIBS}" "${FO_RENDER_SYSTEM_LIBS}" "${FO_RENDER_LIBS}" "${CMAKE_DL_LIBS}" )
endif()

if( FONLINE_CODE_COVERAGE )
	StatusMessage( "+ FOnlineCodeCoverage" )
	list( APPEND FO_APPLICATIONS_GROUP "FOnlineCodeCoverage" )
	add_executable( FOnlineCodeCoverage ${FO_TOOLS_SOURCE} ${FO_SERVER_SOURCE} "Source/Server/AppGuiStub.cpp" ${FO_CLIENT_SOURCE} ${FO_COMMON_SOURCE} "Source/Common/MessageBox.cpp" "Source/Applications/ServerApp.cpp" "Source/Applications/ServerServiceApp.cpp" "Source/Applications/ServerDaemonApp.cpp" "Source/Applications/ServerHeadlessApp.cpp" "Source/Applications/ClientApp.cpp" "Source/Applications/MapperApp.cpp" "Source/Applications/BakerApp.cpp" )
	set_target_properties( FOnlineCodeCoverage PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_DEBUG ${FO_TESTS_OUTPUT}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${FO_TESTS_OUTPUT} RUNTIME_OUTPUT_DIRECTORY_RELEASE ${FO_TESTS_OUTPUT} )
	set_target_properties( FOnlineCodeCoverage PROPERTIES OUTPUT_NAME "FOnlineCodeCoverage${FO_NON_CLIENT_POSTFIX}" )
	set_target_properties( FOnlineCodeCoverage PROPERTIES COMPILE_DEFINITIONS "${FO_BUILD_DEFINES};FO_TESTING" )
	target_compile_options( FOnlineCodeCoverage PRIVATE "-O0;-fprofile-arcs;-ftest-coverage" )
	set_target_properties( FOnlineCodeCoverage PROPERTIES LINK_FLAGS "--coverage" )
	target_link_libraries( FOnlineCodeCoverage "gcov" "ServerScriptingLib" "ClientScriptingLib" "MapperScriptingLib" "${FO_COMMON_SYSTEM_LIBS}" "${FO_COMMON_LIBS}" "${FO_BAKER_SYSTEM_LIBS}" "${FO_BAKER_LIBS}" "${FO_SERVER_SYSTEM_LIBS}" "${FO_SERVER_LIBS}" "${FO_CLIENT_SYSTEM_LIBS}" "${FO_CLIENT_LIBS}" "${FO_RENDER_SYSTEM_LIBS}" "${FO_RENDER_LIBS}" "${CMAKE_DL_LIBS}" )
endif()

# Contributions
function( AddContent pathPattern )
	set( FO_CONTENT "${FO_CONTENT};${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
endfunction()
function( AddResources packName pathPattern )
	set( FO_RESOURCES "${FO_RESOURCES};${packName} ${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
endfunction()
function( AddRawResources pathPattern )
	set( FO_RAW_RESOURCES "${FO_RAW_RESOURCES};${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
endfunction()
function( AddToSpriteAtlas pathPattern )
	set( FO_SPRITE_ATLAS "${FO_SPRITE_ATLAS};${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
endfunction()
function( AddScriptApi headerPath )
	set( FO_SCRIPT_API "${FO_SCRIPT_API};${FO_CONTRIBUTION_DIR}/${headerPath}" PARENT_SCOPE )
endfunction()
function( AddNativeIncludeDir dir )
	if( FONLINE_NATIVE_SCRIPTING )
		foreach( varDir ${dir} )
			include_directories( "${FO_CONTRIBUTION_DIR}/${varDir}" )
		endforeach()
	endif()
endfunction()
function( AddNativeSource pathPattern )
	if( FONLINE_NATIVE_SCRIPTING )
		file( GLOB sourceFiles "${FO_CONTRIBUTION_DIR}/${pathPattern}" )
		if( FONLINE_BUILD_CLIENT )
			target_sources( FOnline PRIVATE "${sourceFiles}" )
		endif()
		if( FONLINE_BUILD_SERVER )
			target_sources( FOnlineServer PRIVATE "${sourceFiles}" )
		endif()
		if( FONLINE_BUILD_MAPPER )
			target_sources( FOnlineMapper PRIVATE "${sourceFiles}" )
		endif()
		# Todo: fill all targets
	endif()
endfunction()
function( AddAngelScriptSource pathPattern )
	if( FONLINE_ANGELSCRIPT_SCRIPTING )
		set( FO_ANGELSCRIPT_FILES "${FO_ANGELSCRIPT_FILES};${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
	endif()
endfunction()
function( AddMonoAssembly assembly commonRefs serverRefs clientRefs mapperRefs )
	if( FONLINE_MONO_SCRIPTING )
		set( FO_MONO_ASSEMBLIES "${FO_MONO_ASSEMBLIES};${assembly}" PARENT_SCOPE )
		set( MonoAssembly_${assembly}_CommonRefs "${commonRefs}" PARENT_SCOPE )
		set( MonoAssembly_${assembly}_ServerRefs "${serverRefs}" PARENT_SCOPE )
		set( MonoAssembly_${assembly}_ClientRefs "${clientRefs}" PARENT_SCOPE )
		set( MonoAssembly_${assembly}_MapperRefs "${mapperRefs}" PARENT_SCOPE )
	endif()
endfunction()
function( AddMonoSource assembly pathPattern )
	if( FONLINE_MONO_SCRIPTING )
		set( MonoAssembly_${assembly}_Source "${MonoAssembly_${assembly}_Source};${FO_CONTRIBUTION_DIR}/${pathPattern}" PARENT_SCOPE )
	endif()
endfunction()
function( CreateConfig config inheritenConfig )
	set( FO_CONFIGS "${FO_CONFIGS};${config}" PARENT_SCOPE )
	set( Config_${config}_Inheriten ${inheritenConfig} PARENT_SCOPE )
endfunction()
function( TweakConfig config option value )
	list( FIND Config_${config}_Options ${option} optionIndex )
	if( optionIndex EQUAL -1 )
		set( Config_${config}_Options "${Config_${config}_Options};${option}" PARENT_SCOPE )
	endif()
	set( Config_${config}___${option} "${value}" PARENT_SCOPE )
endfunction()
function( CreatePackage package niceName devName author version )
	set( FO_PACKAGES "${FO_PACKAGES};${package}" PARENT_SCOPE )
	set( Package_${package}_NiceName "${niceName}" PARENT_SCOPE )
	set( Package_${package}_DevName "${devName}" PARENT_SCOPE )
	set( Package_${package}_Author "${author}" PARENT_SCOPE )
	set( Package_${package}_Version "${version}" PARENT_SCOPE )
endfunction()
function( AddClientToPackage package platform config packType )
	set( Package_${package}_Parts "${Package_${package}_Parts};Client ${platform} ${config} ${packType}" PARENT_SCOPE )
endfunction()
function( AddServerToPackage package platform config packType )
	set( Package_${package}_Parts "${Package_${package}_Parts};Server ${platform} ${config} ${packType}" PARENT_SCOPE )
endfunction()

set( FO_CONTRIBUTION_DIR ${CMAKE_CURRENT_SOURCE_DIR} )
AddNativeIncludeDir( "Source/Scripting/Native" )
AddNativeSource( "Source/Scripting/Native/*.cpp" )
AddAngelScriptSource( "Source/Scripting/AngelScript/*.fos" )
AddMonoAssembly( "FOnline" "System System.Core System.Data System.Xml" "" "" "" )
AddMonoSource( "FOnline" "Source/Scripting/Mono/*.cs" )
AddResources( "Core" "Resources/Core/**" )
GenerateInclude( "CustomScriptApi" )

if( NOT "$ENV{FO_CMAKE_CONTRIBUTION}" STREQUAL "" OR NOT "${FONLINE_CMAKE_CONTRIBUTION}" STREQUAL "" )
	StatusMessage( "Contributions:" )
	foreach( inc ${FONLINE_CMAKE_CONTRIBUTION} )
		get_filename_component( FO_CONTRIBUTION_DIR ${inc} DIRECTORY )
		StatusMessage( "+ ${inc}" )
		include( ${inc} )
	endforeach()
	foreach( inc $ENV{FO_CMAKE_CONTRIBUTION} )
		get_filename_component( FO_CONTRIBUTION_DIR ${inc} DIRECTORY )
		StatusMessage( "+ ${inc}" )
		include( ${inc} )
	endforeach()
endif()

foreach( headerPath ${FO_SCRIPT_API} )
	AddGeneratedIncludeLine( "CustomScriptApi" "#include \"${headerPath}\"" )
endforeach()

# Generate packaging information
if( FONLINE_GENERATE_PACKAGING_INFO )
	function( DeleteFiles pattern )
		file( GLOB files "${pattern}" )
		foreach( file ${files} )
			file( REMOVE "${file}" )
		endforeach()
	endfunction()
	DeleteFiles( "${FO_TOOLS_OUTPUT}/*.fopack" )
	DeleteFiles( "${FO_TOOLS_OUTPUT}/*.fobake" )
	DeleteFiles( "${FO_TOOLS_OUTPUT}/*.focfg" )
	foreach( entry ${FO_CONTENT} )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "[Content]\nPath = ${entry}\n\n" )
	endforeach()
	foreach( entry ${FO_RESOURCES} )
		string( FIND "${entry}" " " sep )
		string( SUBSTRING "${entry}" 0 ${sep} resName )
		string( SUBSTRING "${entry}" ${sep} -1 resPath )
		string( STRIP "${resPath}" resPath )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "[Resources]\nName = ${resName}\nPath = ${resPath}\nType = Packed\n\n" )
	endforeach()
	foreach( entry ${FO_RAW_RESOURCES} )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "[Resources]\nPath = ${entry}\nType = Raw\n\n" )
	endforeach()
	foreach( entry ${FO_ANGELSCRIPT_FILES} )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "[AngelScript]\nSource = ${entry}\n\n" )
	endforeach()
	foreach( entry ${FO_MONO_ASSEMBLIES} )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "[Mono]\nAssembly = ${entry}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "CommonRefs = ${MonoAssembly_${entry}_CommonRefs}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "ServerRefs = ${MonoAssembly_${entry}_ServerRefs}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "ClientRefs = ${MonoAssembly_${entry}_ClientRefs}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "MapperRefs = ${MonoAssembly_${entry}_MapperRefs}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "Source = ${MonoAssembly_${entry}_Source}\n" )
		file( APPEND "${FO_TOOLS_OUTPUT}/Default.fobake" "\n" )
	endforeach()
	foreach( entry ${FO_CONFIGS} )
		set( configOptions "" )
		set( inheritedWritten YES )
		set( curConfig ${entry} )
		while( NOT "${curConfig}" STREQUAL "" )
			foreach( option ${Config_${curConfig}_Options} )
				list( FIND configOptions ${option} optionIndex )
				if( optionIndex EQUAL -1 )
					if( NOT inheritedWritten )
						file( APPEND "${FO_TOOLS_OUTPUT}/${entry}.focfg" "\n# Inherited from '${curConfig}':\n" )
						set( inheritedWritten YES )
					endif()
					list( APPEND configOptions ${option} )
					file( APPEND "${FO_TOOLS_OUTPUT}/${entry}.focfg" "${option} = ${Config_${curConfig}___${option}}\n" )
				endif()
			endforeach()
			set( curConfig ${Config_${curConfig}_Inheriten} )
			if( NOT "${curConfig}" STREQUAL "" )
				set( inheritedWritten NO )
			endif()
		endwhile()
	endforeach()
	foreach( entry ${FO_PACKAGES} )
		file( APPEND "${FO_TOOLS_OUTPUT}/${entry}.fopack" "" )
		foreach( part ${Package_${entry}_Parts} )
			file( APPEND "${FO_TOOLS_OUTPUT}/${entry}.fopack" "${part}\n" )
		endforeach()
	endforeach()
endif()

# Flush generated includes on disk
FinalizeGeneratedIncludes()

# Setup targets grouping
set_property( GLOBAL PROPERTY USE_FOLDERS ON )
set_property( TARGET ${FO_APPLICATIONS_GROUP} PROPERTY FOLDER "Applications" )
set_property( TARGET ${FO_CORE_LIBS_GROUP} PROPERTY FOLDER "CoreLibs" )
set_property( TARGET ${FO_COMMON_LIBS} ${FO_BAKER_LIBS} ${FO_SERVER_LIBS} ${FO_CLIENT_LIBS} ${FO_RENDER_LIBS} PROPERTY FOLDER "ThirdParty" )

# Print cached variables
if( FONLINE_VERBOSE_BUILD )
	get_cmake_property( FO_CACHE_VARIABLES CACHE_VARIABLES )
	list( SORT FO_CACHE_VARIABLES )

	StatusMessage( "Forced variables:" )
	foreach( varName ${FO_CACHE_VARIABLES} )
		get_property( str CACHE ${varName} PROPERTY HELPSTRING )
		get_property( type CACHE ${varName} PROPERTY TYPE )
		string( FIND "${str}" "Forced by FOnline" forced )
		if( NOT "${forced}" STREQUAL "-1" )
			StatusMessage( "- ${varName}: '${${varName}}' type: '${type}'" )
		endif()
	endforeach()

	StatusMessage( "Default variables:" )
	foreach( varName ${FO_CACHE_VARIABLES} )
		get_property( str CACHE ${varName} PROPERTY HELPSTRING )
		get_property( type CACHE ${varName} PROPERTY TYPE )
		string( FIND "${str}" "Forced by FOnline" forced )
		if( "${forced}" STREQUAL "-1" AND NOT "${type}" STREQUAL "INTERNAL" AND NOT "${type}" STREQUAL "STATIC" AND NOT "${type}" STREQUAL "UNINITIALIZED" )
			StatusMessage( "- ${varName}: '${${varName}}' docstring: '${str}' type: '${type}'" )
		endif()
	endforeach()
endif()
